module upper_bc_file

  use shr_kind_mod,  only: r8 => shr_kind_r8
  use shr_kind_mod,  only: cx => shr_kind_cx ! 512
  use cam_logfile,   only: iulog
  use spmd_utils,    only: masterproc
  use cam_abortutils,only: endrun
  use cam_history,   only: addfld, horiz_only, outfld, fieldname_len
  !use cam_history,   only: add_default

  use tracer_data,   only: trfld,trfile,MAXTRCRS

  implicit none
  private

  public :: upper_bc_file_readnl
  public :: upper_bc_file_init
  public :: upper_bc_file_adv
  public :: upper_bc_file_get
  public :: upper_bc_file_specified

  logical, protected :: upper_bc_file_specified = .false.

  character(len=cx) :: ubc_file_path = 'NONE'
  character(len=32) :: ubc_file_input_type = 'NONE'
  integer :: ubc_file_cycle_yr  = -huge(1)
  integer :: ubc_file_fixed_ymd = -huge(1)
  integer :: ubc_file_fixed_tod = -huge(1)


  type(trfld), pointer :: fields(:)
  type(trfile)         :: file

  integer :: num_ubc_flds = 0
  real(r8), allocatable :: ubc_fact(:)
  character(len=fieldname_len), allocatable :: hist_names(:)

contains

  !---------------------------------------------------------------------------
  !---------------------------------------------------------------------------
  subroutine upper_bc_file_readnl(nlfile)
    use namelist_utils, only : find_group_name
    use spmd_utils, only : mpicom, masterprocid, mpi_character, mpi_integer

    character(len=*), intent(in) :: nlfile

    integer :: unitn, ierr, n, ndx
    character(len=*), parameter :: prefix = 'upper_bc_file_readnl: '

    namelist /upper_bc_file_opts/ ubc_file_path, ubc_file_input_type
    namelist /upper_bc_file_opts/ ubc_file_cycle_yr, ubc_file_fixed_ymd, ubc_file_fixed_tod

    if (masterproc) then
       ! read namelist
       open( newunit=unitn, file=trim(nlfile), status='old' )
       call find_group_name(unitn, 'upper_bc_file_opts', status=ierr)
       if (ierr == 0) then
          read(unitn, upper_bc_file_opts, iostat=ierr)
          if (ierr /= 0) then
             call endrun(prefix//'upper_bc_file_opts: ERROR reading namelist')
          end if
       end if
       close(unitn)
    end if

    call mpi_bcast(ubc_file_path, len(ubc_file_path), mpi_character, masterprocid, mpicom, ierr)
    call mpi_bcast(ubc_file_input_type, len(ubc_file_input_type), mpi_character, masterprocid, mpicom, ierr)
    call mpi_bcast(ubc_file_fixed_ymd, 1, mpi_integer, masterprocid, mpicom, ierr)
    call mpi_bcast(ubc_file_fixed_tod, 1, mpi_integer, masterprocid, mpicom, ierr)
    call mpi_bcast(ubc_file_cycle_yr,  1, mpi_integer, masterprocid, mpicom, ierr)

    upper_bc_file_specified = .not. ubc_file_path == 'NONE'

    if (masterproc) then
       write(iulog,*) prefix//'upper_bc_file_specified: ',upper_bc_file_specified
       write(iulog,*) prefix//'ubc_file_path = '//trim(ubc_file_path)
       write(iulog,*) prefix//'ubc_file_input_type = '//trim(ubc_file_input_type)
       write(iulog,*) prefix//'ubc_file_cycle_yr = ',ubc_file_cycle_yr
       write(iulog,*) prefix//'ubc_file_fixed_ymd = ',ubc_file_fixed_ymd
       write(iulog,*) prefix//'ubc_file_fixed_tod = ',ubc_file_fixed_tod
    end if

  end subroutine upper_bc_file_readnl

  !---------------------------------------------------------------------------
  !---------------------------------------------------------------------------
  subroutine upper_bc_file_init( flds_list )
    use tracer_data, only: trcdata_init
    use constituents,only: cnst_get_ind, cnst_mw
    use physconst,   only: mwdry
    use string_utils,only: to_lower

    character(len=*) :: flds_list(:)

    integer :: m, ndx

    num_ubc_flds = size(flds_list)
    upper_bc_file_specified = upper_bc_file_specified .and. (num_ubc_flds>0)

    if (.not.upper_bc_file_specified) return

    allocate( ubc_fact(num_ubc_flds) )
    ubc_fact(:) = -huge(1._r8)

    allocate(file%in_pbuf(num_ubc_flds))
    file%in_pbuf(:) = .false.

    call trcdata_init( flds_list, ubc_file_path, ' ', ' ', fields, file, .false., &
         ubc_file_cycle_yr, ubc_file_fixed_ymd, ubc_file_fixed_tod, ubc_file_input_type)

    file%top_bndry = .true.

    allocate(hist_names(num_ubc_flds))
    hist_names = ' '

    do m = 1,num_ubc_flds

       call cnst_get_ind(trim(fields(m)%fldnam), ndx, abort=.true.)

       select case ( to_lower(trim(fields(m)%units)) )
       case ('k','kg/kg','mmr')
          ubc_fact(m) = 1._r8
       case ('mol/mol','mole/mole','vmr')
          ubc_fact(m) = cnst_mw(ndx)/mwdry
       case default
          call endrun('upper_bc_file_get: units are not recognized')
       end select

       hist_names(m) = trim(fields(m)%fldnam)//'_fubc'
       if ( to_lower(trim(fields(m)%units)) == 'k' ) then
          call addfld(hist_names(m), horiz_only, 'I', 'K', trim(fields(m)%fldnam)//' at upper boundary' )
       else
          call addfld(hist_names(m), horiz_only, 'I', 'kg/kg', trim(fields(m)%fldnam)//' at upper boundary' )
       end if
       !call add_default(hist_names(m), 2, ' ' )

    end do

  end subroutine upper_bc_file_init

  !---------------------------------------------------------------------------
  !---------------------------------------------------------------------------
  subroutine upper_bc_file_adv(pbuf2d, state)
    use tracer_data,    only : advance_trcdata
    use physics_types,  only : physics_state
    use physics_buffer, only : physics_buffer_desc

    ! args
    type(physics_state),    intent(in) :: state(:)
    type(physics_buffer_desc), pointer :: pbuf2d(:,:)

    if (.not.upper_bc_file_specified) return

    call advance_trcdata( fields, file, state, pbuf2d )

  end subroutine upper_bc_file_adv

  !---------------------------------------------------------------------------
  !---------------------------------------------------------------------------
  subroutine upper_bc_file_get(lchnk, ncol, val)

    integer, intent(in) :: ncol, lchnk
    real(r8), intent(out) :: val(:,:)

    integer :: m

    if (.not.upper_bc_file_specified) return

    do m = 1,num_ubc_flds
       val(:ncol,m) = ubc_fact(m)*fields(m)%data(:ncol,1,lchnk)
       call outfld( trim(hist_names(m)), val(:ncol,m), ncol, lchnk )
    enddo


  end subroutine upper_bc_file_get

end module upper_bc_file
